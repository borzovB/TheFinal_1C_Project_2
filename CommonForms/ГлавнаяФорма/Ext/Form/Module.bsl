
// Главная панель, где отображаются поездки, выбираются команды для начала, окончания поездок или 
// записи на поездку в зависимости от роли

// Глобальная переменная для управления обновлением
&НаКлиенте
Перем ОбновлениеВремениАктивно Экспорт;

// Флаг ожидания подключения
&НаКлиенте
Перем ПодключениеОжидается Экспорт; 

// Обработчик команды показывающий текущее местоположение устройства
&НаКлиенте
Процедура ПоказатьМоёМестоположение(Команда)
	
	// Вызов процедуры открывающая карту
	ОпределениеПоложения.ОткрытьКартуСМестоположением();

КонецПроцедуры

// Возврат на форму с авторизацией, закрывается главная панель
&НаКлиенте
Процедура ВернутьсяВПанельВхода(Команда)
	Закрыть();
	ОткрытьФорму("ОбщаяФорма.Авторизация");
КонецПроцедуры

// Осуществление обмена данными с сервером
Процедура СинхронизироватьНажатие()
    СинхронизацияМобильногоУстройства.СинхронизироватьДанныеНажатие();
КонецПроцедуры

// Создание главной панели на сервере, осуществляется выбор вида формы в зависимости от роли 
// водитель или пассажир, с соответсвующим набором команд
&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	СинхронизироватьНажатие();
	ОбновитьДатуВремяНаСервере();
	Если Параметры.Свойство("Логин") Тогда
        Логин = Параметры.Логин;
    КонецЕсли;
    
    Если Параметры.Свойство("Пароль") Тогда
        Пароль = Параметры.Пароль;
	КонецЕсли;
	
	// Переменная для хранения найденного пользователя
	НайденныйПользовательРоль = Неопределено;

	// Перебор пользователей
	Выборка = Справочники.Пользователи.Выбрать();
	Пока Выборка.Следующий() Цикл
    	Если Не ПустаяСтрока(Выборка.Логин) И Не ПустаяСтрока(Выборка.Пароль) Тогда
        	// Проверка хэша логина
        	Если СервисАвторизации.ПроверитьПароль(Выборка.Логин, Логин) Тогда
            	// Проверка хэша пароля
            	Если СервисАвторизации.ПроверитьПароль(Выборка.Пароль, Пароль) Тогда
                	НайденныйПользовательРоль = Выборка;
                	Прервать; // Прерываем цикл, если пользователь найден
            	КонецЕсли;
        	КонецЕсли;
    	КонецЕсли;
	КонецЦикла;

	// Вывод данных пользователя
	Если НайденныйПользовательРоль <> Неопределено Тогда
    	Элементы.НадписьПриветствие.Заголовок = СтрШаблон("Добро пожаловать, %1!", 
                                                    НайденныйПользовательРоль.Наименование);
		Элементы.НадписьРоль.Заголовок = СтрШаблон("Ваша роль: %1",  
                                                    НайденныйПользовательРоль.Роль);
	КонецЕсли;
												
	// Управление видимостью элементов в зависимости от роли
	Если НайденныйПользовательРоль <> Неопределено И НайденныйПользовательРоль.Роль = Перечисления.Роль.Водитель Тогда
		Элементы.ЗаписанныеПоездки.Видимость = Ложь;
        // Показываем элементы для водителя
        Элементы.ДатаВыбора.Видимость = Истина;
        Элементы.ТаблицаПоездок.Видимость = Истина;
		Элементы.ЗаписатьсяНаПоездку.Видимость = Ложь;
        // Установка текущей даты по умолчанию
        Если Не ЗначениеЗаполнено(ДатаВыбора) Тогда
            ДатаВыбора = НачалоДня(ТекущаяДата());
        КонецЕсли;
        
        ТекущийПользователь = НайденныйПользовательРоль.Наименование;
		ПользовательРоль = "Водитель";
		Элементы.НоваяДекор.Видимость = Ложь;
		Элементы.ОтменитьПоездку.Видимость = Ложь;
		Элементы.ОтобразитьМаршрут.Видимость = Ложь;
		Элементы.РасширеннаяИнформация.Видимость = Ложь;
		Элементы.СоздатьНапоминаниеНажатие.Видимость = Ложь;
		Элементы.ПоказатьНаКартеПоследнееМесто.Видимость = Ложь;
        // Обновление таблицы поездок
        ОбновитьТаблицуПоездок(ТекущийПользователь);
	Иначе
		Элементы.ДекорацияПоездкиПассажир.Видимость = Ложь;
		ПользовательРоль = "Пассажир";
        Элементы.ТаблицаПоездок.Видимость = Ложь;
		Элементы.КнопкаЗавершитьПоездкуНажатие.Видимость = Ложь;
		Элементы.КнопкаНачатьПоездку.Видимость = Ложь;
		Элементы.КнопкаОткрытьПоездкуНажатие.Видимость = Ложь;
		Элементы.ЗаписанныеПоездки.Видимость = Истина;
		
		// Установка текущей даты по умолчанию
        Если Не ЗначениеЗаполнено(ДатаВыбора) Тогда
            ДатаВыбора = НачалоДня(ТекущаяДата());
        КонецЕсли;
        
       ТекущийПользователь = НайденныйПользовательРоль.Наименование;
        // Обновление таблицы поездок
        ОбновитьДанныеНаФорме(ТекущийПользователь);

    КонецЕсли;
	
КонецПроцедуры

// Обновление данных на форме
&НаСервере
Процедура ОбновитьДанныеНаФорме(ТекущийПользователь)
	ЗаписанныеПоездки.Очистить();
	
	// Запрос к документу Поездка
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ПоездкаЗабронированныеМеста.Ссылка КАК Ссылка,
		|	ПоездкаЗабронированныеМеста.Ссылка КАК Поездка,
		|	ЕСТЬNULL(ПоездкаЗабронированныеМеста.ОстановкаПосадки.Наименование, """") КАК НачальнаяТочка,
		|	ЕСТЬNULL(ПоездкаЗабронированныеМеста.ОстановкаВысадки.Наименование, """") КАК КонечнаяТочка,
		|	ПоездкаЗабронированныеМеста.Ссылка.ПоездкаНачалась КАК ПоездкаНачалась,
		|	ПоездкаЗабронированныеМеста.Ссылка.ПоездкаЗавершена КАК ПоездкаЗавершена
		|ИЗ
		|	Документ.Поездка.ЗабронированныеМеста КАК ПоездкаЗабронированныеМеста
		|ГДЕ
		|	ПоездкаЗабронированныеМеста.Пассажир = &ТекущийПользователь
		|	И ПоездкаЗабронированныеМеста.БроньОтменена = ЛОЖЬ
		|	И ПоездкаЗабронированныеМеста.Ссылка.ДатаПоездки >= &ДатаНачала
		|	И ПоездкаЗабронированныеМеста.Ссылка.ДатаПоездки < &ДатаКонец";
	
	Запрос.УстановитьПараметр("ТекущийПользователь", ТекущийПользователь);
	Запрос.УстановитьПараметр("ДатаНачала", НачалоДня(ДатаВыбора));
	Запрос.УстановитьПараметр("ДатаКонец", КонецДня(ДатаВыбора));	
	// Выполнение запроса и заполнение таблицы
	Результат = Запрос.Выполнить();
	Если НЕ Результат.Пустой() Тогда
		Выборка = Результат.Выбрать();
		Пока Выборка.Следующий() Цикл
			НоваяСтрока = ЗаписанныеПоездки.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяСтрока, Выборка);
			НоваяСтрока.Статус = РассчитатьСтатус(Выборка.ПоездкаНачалась, Выборка.ПоездкаЗавершена);
		КонецЦикла;
		// Скрываем декорацию, если есть поездки
		Элементы.НоваяДекор.Видимость = Ложь;
		Элементы.ЗаписанныеПоездки.Видимость = Истина;
	Иначе
        // Показываем декорацию с текстом, если поездок нет
		Если ПользовательРоль = "Пассажир" Тогда
        	
        	Элементы.НоваяДекор.Видимость = Истина;
		
		Иначе
        	
        	Элементы.ДекорацияПоездкиПассажир.Видимость = Ложь;

		КонецЕсли;
		
		Элементы.ЗаписанныеПоездки.Видимость = Ложь;
	КонецЕсли;
КонецПроцедуры

// Расчёт статуса поездки
&НаСервере
Функция РассчитатьСтатус(ПоездкаНачалась, ПоездкаЗавершена)
	Если ПоездкаНачалась И ПоездкаЗавершена Тогда
		Возврат "Поездка завершена";
	ИначеЕсли ПоездкаНачалась И НЕ ПоездкаЗавершена Тогда
		Возврат "В пути";
	Иначе
		Возврат "Поездка не началась";
	КонецЕсли;
КонецФункции

// Обработчик оповещения в главной форме
&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
    Если ИмяСобытия = "ОбновитьТаблицуПоездок" Тогда
        // Перечитываем данные и обновляем таблицу для текущего пользователя
        ОбновитьТаблицуПоездок(ТекущийПользователь);
	КонецЕсли;
	
	Если ИмяСобытия = "ОбновитьДоступныеПоездки" Тогда
        // Перечитываем данные и обновляем таблицу для текущего пользователя
        ОбновитьДанныеНаФорме(ТекущийПользователь);
    КонецЕсли;

КонецПроцедуры

// Процедура обновления таблицы поездок
&НаСервере
Процедура ОбновитьТаблицуПоездок(ТекущийПользователь)
    ТаблицаПоездок.Очистить();
    
    // Запрос к документу Поездка с корректной группировкой
    Запрос = Новый Запрос;
    Запрос.Текст = 
    "ВЫБРАТЬ
    |   Поездка.Ссылка КАК Поездка,
    |   Поездка.ПоездкаНачалась,
    |   Поездка.ПоездкаЗавершена,
    |   МИНИМУМ(ЕСТЬNULL(ПоездкаМаршрутПоездки.Время, ДАТАВРЕМЯ(1,1,1))) КАК ВремяНачала,
    |   МАКСИМУМ(ЕСТЬNULL(ПоездкаМаршрутПоездки.Время, ДАТАВРЕМЯ(1,1,1))) КАК ВремяОкончания
    |ИЗ
    |   Документ.Поездка КАК Поездка
    |       ЛЕВОЕ СОЕДИНЕНИЕ Документ.Поездка.МаршрутПоездки КАК ПоездкаМаршрутПоездки
    |       ПО Поездка.Ссылка = ПоездкаМаршрутПоездки.Ссылка
    |ГДЕ
    |   Поездка.ДатаПоездки >= &ДатаНачала
    |   И Поездка.ДатаПоездки < &ДатаКонец
    |   И Поездка.Водитель = &ТекущийПользователь
    |СГРУППИРОВАТЬ ПО
    |   Поездка.Ссылка,
    |   Поездка.ПоездкаНачалась,
    |   Поездка.ПоездкаЗавершена";
    
    // Установка параметров запроса
    Запрос.УстановитьПараметр("ДатаНачала", НачалоДня(ДатаВыбора));
    Запрос.УстановитьПараметр("ДатаКонец", КонецДня(ДатаВыбора));
    Запрос.УстановитьПараметр("ТекущийПользователь", ТекущийПользователь);
    
    // Выполнение запроса и заполнение таблицы
    Результат = Запрос.Выполнить();
    Если Не Результат.Пустой() Тогда
        Выборка = Результат.Выбрать();
        Пока Выборка.Следующий() Цикл
            НоваяСтрока = ТаблицаПоездок.Добавить();
            НоваяСтрока.Поездка = Выборка.Поездка;
            
            // Определение статуса на основе флагов
            Если Выборка.ПоездкаНачалась И Выборка.ПоездкаЗавершена Тогда
                НоваяСтрока.Статус = "Поездка завершена";
            ИначеЕсли Выборка.ПоездкаНачалась И Не Выборка.ПоездкаЗавершена Тогда
                НоваяСтрока.Статус = "В пути";
            Иначе
                НоваяСтрока.Статус = "Поездка не началась";
            КонецЕсли;
            
            НоваяСтрока.ВремяНачала = Выборка.ВремяНачала;
            НоваяСтрока.ВремяОкончания = Выборка.ВремяОкончания;
        КонецЦикла;
        // Скрываем декорацию, если есть поездки
        Элементы.ДекорацияПоездкиПассажир.Видимость = Ложь;
		Элементы.НоваяДекор.Видимость = Ложь;
		Элементы.ТаблицаПоездок.Видимость = Истина;
    Иначе
        // Показываем декорацию с текстом, если поездок нет
		Если ПользовательРоль = "Водитель" Тогда
        	
        	Элементы.ДекорацияПоездкиПассажир.Видимость = Истина;
		
		Иначе
        	
        	Элементы.ДекорацияПоездкиПассажир.Видимость = Ложь;

    	КонецЕсли;

		Элементы.ТаблицаПоездок.Видимость = Ложь;
	КонецЕсли;
	
КонецПроцедуры

// Обработчик изменения даты
&НаКлиенте
Процедура ДатаВыбораПриИзменении(Элемент)
   	// Управление видимостью элементов в зависимости от роли
	Если ПользовательРоль = "Водитель" Тогда
        // Обновление таблицы поездок
        ОбновитьТаблицуПоездок(ТекущийПользователь);
		
	Иначе
        // Обновление таблицы поездок
        ОбновитьДанныеНаФорме(ТекущийПользователь);

    КонецЕсли;

КонецПроцедуры

// Обновление даты на главной форме, чтобы пользователь знал текущую дату
&НаКлиенте
Процедура ПриОткрытии(Отказ)
	ПодключитьОбработчикОжидания("ОбновитьДатуВремяНаКлиенте", 300);
	// Инициализация
	ПодключениеОжидается = Ложь;
	// Запускаем проверку сети и таймер
	ПроверитьСетьИОбновить();
КонецПроцедуры

// отключает обработчик ожидания
&НаКлиенте
Процедура ПриЗакрытии(ЗавершениеРаботы)
	// Отключаем обработчик ожидания обновления даты
	ОтключитьОбработчикОжидания("ПроверитьСетьИОбновить");
КонецПроцедуры

// Обновление даты на главной форме каждые 5 секунд
&НаКлиенте
Процедура ПроверитьСетьИОбновить() Экспорт
	#Если МобильноеПриложениеКлиент Тогда
		// Проверка доступности сети
		ПодключениеОжидается = Ложь;
		// Обновляем таблицу и записываем координаты
		ОбновитьИЗаписатьКоординаты();
		// Устанавливаем таймер на 5 секунд
		ПодключитьОбработчикОжидания("ПроверитьСетьИОбновить", 10, Истина);
	#Иначе
		// Для немобильного клиента сразу обновляем
		ОбновитьИЗаписатьКоординаты();
		ПодключитьОбработчикОжидания("ПроверитьСетьИОбновить", 10, Истина);
	#КонецЕсли
КонецПроцедуры

// Обработьчик фиксации текущего местоположения водителя
&НаКлиенте
Процедура ОбновитьИЗаписатьКоординаты()
	// Обновляем таблицу поездок
	ОбновитьТаблицуПоездок(ТекущийПользователь);
	
	// Проверяем поездки со статусом "В пути"
	Для Каждого СтрокаТаблицы Из ТаблицаПоездок Цикл
		Если СтрокаТаблицы.Статус = "В пути" Тогда
			// Получаем текущие координаты
			Координаты = ПолучитьТекущиеКоординаты();
			Если Координаты <> Неопределено Тогда
				// Записываем координаты в регистр
				ЗаписатьКоординатыВРегистр(СтрокаТаблицы.Поездка, Координаты);
			КонецЕсли;
		КонецЕсли;
	КонецЦикла;
КонецПроцедуры

// Определение геопозиции локальногг устройства
&НаКлиенте
Функция ПолучитьТекущиеКоординаты()
	#Если МобильноеПриложениеКлиент Тогда
		Попытка
			Провайдер = СредстваГеопозиционирования.ПолучитьСамогоЭнергоЭкономичногоПровайдера();
			Если СредстваГеопозиционирования.ОбновитьМестоположение(Провайдер.Имя, 60) Тогда
				ДанныеМестоположения = СредстваГеопозиционирования.ПолучитьПоследнееМестоположение(Провайдер.Имя);
				Возврат ДанныеМестоположения.Координаты;
			КонецЕсли;
		Исключение
			
			ТекстСообщения = "Ошибка получения координат: " + ОписаниеОшибки();
			ПоказатьПредупреждение(, НСтр("ru='" + ТекстСообщения + "'"));
			
		КонецПопытки;
	#КонецЕсли
	Возврат Неопределено;
КонецФункции

// Создание записи с координатами водителя, данная фукнкция доступна для водителя
&НаСервереБезКонтекста
Процедура ЗаписатьКоординатыВРегистр(Поездка, Координаты)
	// Создаём запись в регистре сведений
	Запись = РегистрыСведений.КоординатыПоездок.СоздатьМенеджерЗаписи();
	Запись.Поездка = Поездка;
	Запись.ВремяГеопозиции = ТекущаяДатаСеанса();
	Запись.Координаты = Новый ХранилищеЗначения(Координаты);
	
	Попытка
		Запись.Записать(Истина);
	Исключение
		Сообщение = Новый СообщениеПользователю;
		Сообщение.Текст = НСтр("ru='Ошибка записи координат: '") + ОписаниеОшибки();
		Сообщение.Сообщить();
	КонецПопытки;
КонецПроцедуры

// Проверка доступа к серверу
&НаСервереБезКонтекста
Функция ДоступноПодключениеКИнтернету()
	Возврат СетевоеСоединение.ПроверитьСвязьССервером();
КонецФункции

// Обновление даты и времени на сервере
&НаСервере
Процедура ОбновитьДатуВремяНаСервере()
    Элементы.НадписьДатаВремя.Заголовок = "Текущая дата: " + Формат(ТекущаяДата(), "ДФ='dd.MM.yyyy'"); 
КонецПроцедуры

// Обновление даты и времени на клиенте
&НаКлиенте
Процедура ОбновитьДатуВремяНаКлиенте()
    Элементы.НадписьДатаВремя.Заголовок = "Текущая дата: " + Формат(ТекущаяДата(), "ДФ='dd.MM.yyyy'"); 
КонецПроцедуры

// Обработчик нажатия кнопки "Начать поездку"
&НаКлиенте
Процедура КнопкаНачатьПоездкуНажатие(Элемент)
    // Получаем текущую строку таблицы
    ТекущиеДанные = Элементы.ТаблицаПоездок.ТекущиеДанные;
    Если ТекущиеДанные = Неопределено Тогда
        ПоказатьПредупреждение(, "Выберите поездку для начала.");
        Возврат;
	КонецЕсли;
	
	Если ПоездкаВБудущем(ТекущиеДанные.Поездка) Тогда
		
		Сообщить("Время поездки еще не пришло");
		
	Иначе
		
    	// Вызываем серверную процедуру для изменения статуса документа
    	НачатьПоездкуНаСервере(ТекущиеДанные.Поездка);
    
    	// Обновляем таблицу на форме
    	ОбновитьТаблицуПоездок(ТекущийПользователь);

	КонецЕсли;
    
КонецПроцедуры

// Проверяет, является ли дата поездки будущей относительно текущей даты
&НаСервере
Функция ПоездкаВБудущем(Поездка)
    
    // Получаем текущую дату без времени
    ТекущаяДата = НачалоДня(ТекущаяДата());
    
    // Запрос для получения даты документа
    Запрос = Новый Запрос;
    Запрос.Текст = 
        "ВЫБРАТЬ
        |	Поездка.ДатаПоездки КАК ДатаДокумента
        |ИЗ
        |	Документ.Поездка КАК Поездка
        |ГДЕ
        |	Поездка.Ссылка = &Поездка";
    Запрос.УстановитьПараметр("Поездка", Поездка);
    
    Результат = Запрос.Выполнить();
    
    // Проверяем, есть ли данные
    Если Результат.Пустой() Тогда
        Возврат Ложь; 
    КонецЕсли;
    
    Выборка = Результат.Выбрать();
    Выборка.Следующий();
    
    // Получаем дату документа без времени
    ДатаДокумента = НачалоДня(Выборка.ДатаДокумента);
    
    // Проверяем, заполнена ли дата
    Если Не ЗначениеЗаполнено(ДатаДокумента) Тогда
        Возврат Ложь; 
    КонецЕсли;
    
    // Сравниваем с текущей датой
    Если ДатаДокумента > ТекущаяДата Тогда
        Возврат Истина; 
    Иначе
        Возврат Ложь; // Поездка в прошлом или настоящем
    КонецЕсли;
    
КонецФункции

// Серверная процедура для изменения статуса документа
&НаСервере
Процедура НачатьПоездкуНаСервере(ПоездкаСсылка)
    // Получаем объект документа
    ДокументОбъект = ПоездкаСсылка.ПолучитьОбъект();
    Если ДокументОбъект <> Неопределено Тогда
        // Устанавливаем флаг начала поездки и текущее время
        ДокументОбъект.ПоездкаНачалась = Истина;         
        // Сохраняем документ
        Попытка
            ДокументОбъект.Записать(РежимЗаписиДокумента.Запись);
        Исключение
            Сообщить("Ошибка при начале поездки: " + ОписаниеОшибки());
        КонецПопытки;
    Иначе
        Сообщить("Документ не найден.");
    КонецЕсли;
КонецПроцедуры

// Обработчик нажатия кнопки "Завершить поездку"
&НаКлиенте
Процедура КнопкаЗавершитьПоездкуНажатие(Элемент)
    // Получаем текущую строку таблицы
    ТекущиеДанные = Элементы.ТаблицаПоездок.ТекущиеДанные;
    Если ТекущиеДанные = Неопределено Тогда
        ПоказатьПредупреждение(, "Выберите поездку для завершения.");
        Возврат;
    КонецЕсли;
    
    // Проверяем, началась ли поездка
    Если Не ТекущиеДанные.Статус = "В пути" Тогда
        ПоказатьПредупреждение(, "Поездку можно завершить только если она находится в пути.");
        Возврат;
    КонецЕсли;
    
    // Вызываем серверную процедуру для изменения статуса документа
    ЗавершитьПоездкуНаСервере(ТекущиеДанные.Поездка);
    
    // Обновляем таблицу на форме
    ОбновитьТаблицуПоездок(ТекущийПользователь);
КонецПроцедуры

// Серверная процедура для изменения статуса документа на "Поездка завершена"
&НаСервере
Процедура ЗавершитьПоездкуНаСервере(ПоездкаСсылка)
    // Получаем объект документа
    ДокументОбъект = ПоездкаСсылка.ПолучитьОбъект();
    Если ДокументОбъект <> Неопределено Тогда
        // Устанавливаем флаг завершения поездки и текущее время
        ДокументОбъект.ПоездкаЗавершена = Истина;        
        // Сохраняем документ
        Попытка
            ДокументОбъект.Записать(РежимЗаписиДокумента.Запись);
        Исключение
            Сообщить("Ошибка при завершении поездки: " + ОписаниеОшибки());
        КонецПопытки;
    Иначе
        Сообщить("Документ не найден.");
    КонецЕсли;
КонецПроцедуры

// Обработчик нажатия кнопки "Открыть поездку"
&НаКлиенте
Процедура КнопкаОткрытьПоездкуНажатие(Элемент)
    // Получаем текущую строку таблицы
    ТекущиеДанные = Элементы.ТаблицаПоездок.ТекущиеДанные;
    Если ТекущиеДанные = Неопределено Тогда
        ПоказатьПредупреждение(, "Выберите поездку для открытия.");
        Возврат;
    КонецЕсли;
    
    // Открываем форму документа с передачей ссылки на поездку
    ПараметрыФормы = Новый Структура("Ключ", ТекущиеДанные.Поездка);
    ОткрытьФорму("Документ.Поездка.Форма.ФормаДокументаПоездка", ПараметрыФормы, ЭтотОбъект);
КонецПроцедуры

// Обработчик команды, которая показывает маршрут полностью
&НаКлиенте
Процедура ПоказатьМаршрут(Команда)
	
	СинхронизироватьНажатие();
	ТекущаяСтрока = "";
	
	// Показываем декорацию с текстом, если поездок нет
	Если ПользовательРоль = "Водитель" Тогда
        	
        ТекущаяСтрока = Элементы.ТаблицаПоездок.ТекущиеДанные;
		
	Иначе
        	
        ТекущаяСтрока = Элементы.ЗаписанныеПоездки.ТекущиеДанные;

	КонецЕсли;
		
	Если ТекущаяСтрока = Неопределено Тогда
		ПоказатьПредупреждение(, "Выберите поездку в таблице!");
		Возврат;
	КонецЕсли;
	
	Если НЕ ДоступноПодключениеКИнтернету() Тогда
		ПоказатьПредупреждение(, "Нет интернет соединения!");
		Возврат;
	КонецЕсли;

	
	Поездка = ТекущаяСтрока.Поездка;
	
	#Если МобильноеПриложениеКлиент Тогда
		// Получаем данные маршрута
		ДанныеМаршрута = ПолучитьДанныеМаршрута(Поездка);
		Если ДанныеМаршрута = Неопределено ИЛИ ДанныеМаршрута.Количество() = 0 Тогда
			ПоказатьПредупреждение(, "Для выбранной поездки нет данных маршрута!");
			Возврат;
		КонецЕсли;
		
		// Формируем URL для Google Maps
		Результат = ОпределениеПоложения.СоздатьURLМаршрутаМеждуТочками(ДанныеМаршрута);
		URL = Результат.URL;
		Если НЕ ЗначениеЗаполнено(URL) Тогда
			ПоказатьПредупреждение(, "Не удалось построить маршрут: недостаточно точек!");
			Возврат;
		КонецЕсли;
				
		// Пробуем запустить Google Maps
		Запуск = Новый ЗапускПриложенияМобильногоУстройства("android.intent.action.VIEW", URL);
		Попытка
			Запуск.Запустить(Ложь);
		Исключение
			ПоказатьПредупреждение(, "Не удалось открыть Google Maps. Открываем в браузере...");
			Попытка
				ПерейтиПоНавигационнойСсылке(URL);
			Исключение
				ПоказатьПредупреждение(, "Ошибка открытия карты: " + ОписаниеОшибки());
			КонецПопытки;
		КонецПопытки;
	#Иначе
		// Для немобильных клиентов
		ДанныеМаршрута = ПолучитьДанныеМаршрута(Поездка);
		Если ДанныеМаршрута = Неопределено ИЛИ ДанныеМаршрута.Количество() = 0 Тогда
			ПоказатьПредупреждение(, "Для выбранной поездки нет данных маршрута!");
			Возврат;
		КонецЕсли;
		
		Результат = ОпределениеПоложения.СоздатьURLМаршрутаМеждуТочками(ДанныеМаршрута);
		URL = Результат.URL;
		Если НЕ ЗначениеЗаполнено(URL) Тогда
			ПоказатьПредупреждение(, "Не удалось построить маршрут: недостаточно точек!");
			Возврат;
		КонецЕсли;
				
		// Открываем URL в браузере
		Попытка
			ПерейтиПоНавигационнойСсылке(URL);
		Исключение
			ПоказатьПредупреждение(, "Ошибка открытия карты: " + ОписаниеОшибки());
		КонецПопытки;
	#КонецЕсли
КонецПроцедуры

// Функция получает данные маршрута для указанной поездки из табличной части документа "Поездка".
&НаСервереБезКонтекста
Функция ПолучитьДанныеМаршрута(Поездка)
    
    // Создаем новый объект запроса для выполнения SQL-подобного запроса к базе данных.
    Запрос = Новый Запрос;
    
    // Текст запроса к табличной части "МаршрутПоездки" документа "Поездка".
    // Выбираем адресные данные (страна, регион, город, улица, дом, индекс) и время точки маршрута.
    Запрос.Текст = 
    "ВЫБРАТЬ
    |	ПоездкаМаршрутПоездки.Адрес.Страна КАК Страна,
    |	ПоездкаМаршрутПоездки.Адрес.Регион КАК Регион,
    |	ПоездкаМаршрутПоездки.Адрес.Город КАК Город,
    |	ПоездкаМаршрутПоездки.Адрес.Улица КАК Улица,
    |	ПоездкаМаршрутПоездки.Адрес.Дом КАК Дом,
    |	ПоездкаМаршрутПоездки.Адрес.Индекс КАК Индекс,
    |	ПоездкаМаршрутПоездки.Время КАК Время
    |ИЗ
    |	Документ.Поездка.МаршрутПоездки КАК ПоездкаМаршрутПоездки
    |ГДЕ
    |	ПоездкаМаршрутПоездки.Ссылка = &Поездка
    |УПОРЯДОЧИТЬ ПО
    |	ПоездкаМаршрутПоездки.Время";
    
    // Устанавливаем параметр запроса "Поездка" для фильтрации данных по конкретному документу.
    Запрос.УстановитьПараметр("Поездка", Поездка);
    
    // Выполняем запрос и сохраняем результат.
    Результат = Запрос.Выполнить();
    
    // Проверяем, пустой ли результат запроса (нет строк в табличной части).
    Если Результат.Пустой() Тогда
        // Выводим сообщение об ошибке для отладки, указывая, что данные маршрута не найдены.
        Сообщить("Ошибка: Данные маршрута не найдены для поездки " + Поездка);
        // Возвращаем Неопределено, чтобы сигнализировать об отсутствии данных.
        Возврат Неопределено;
    КонецЕсли;
    
    // Создаем массив для хранения данных маршрута.
    ДанныеМаршрута = Новый Массив;
    
    // Получаем выборку из результата запроса для перебора строк.
    Выборка = Результат.Выбрать();
    
    // Перебираем строки выборки.
    Пока Выборка.Следующий() Цикл
        // Проверяем, заполнено ли поле "Город" (обязательное для адреса).
        Если НЕ ЗначениеЗаполнено(Выборка.Город) Тогда
            // Если город не заполнен, выводим сообщение и пропускаем эту точку маршрута.
            Сообщить("Пропущена точка маршрута: город не заполнен");
            Продолжить;
        КонецЕсли;
        
        // Создаем структуру для хранения данных одной точки маршрута.
        СтруктураАдреса = Новый Структура;
        // Заполняем структуру данными из выборки.
        СтруктураАдреса.Вставить("Страна", Выборка.Страна);
        СтруктураАдреса.Вставить("Регион", Выборка.Регион);
        СтруктураАдреса.Вставить("Город", Выборка.Город);
        СтруктураАдреса.Вставить("Улица", Выборка.Улица);
        СтруктураАдреса.Вставить("Дом", Выборка.Дом);
        СтруктураАдреса.Вставить("Индекс", Выборка.Индекс);
        СтруктураАдреса.Вставить("Время", Выборка.Время);
        
        // Добавляем структуру в массив данных маршрута.
        ДанныеМаршрута.Добавить(СтруктураАдреса);
    КонецЦикла;
    
    // Проверяем, содержит ли массив данные после обработки.
    Если ДанныеМаршрута.Количество() = 0 Тогда
        // Если массив пуст (например, все точки маршрута имели пустой город), выводим сообщение об ошибке.
        Сообщить("Ошибка: Нет валидных точек маршрута");
        // Возвращаем Неопределено, чтобы сигнализировать об отсутствии валидных данных.
        Возврат Неопределено;
    КонецЕсли;
    
    // Возвращаем массив с данными маршрута.
    Возврат ДанныеМаршрута;
КонецФункции

// Клиентская процедура для обработки нажатия кнопки, после чего на экране появляется карта с
// маршрутом и текущим местоположением водителя, только для поездок со статусом "В пути"
&НаКлиенте
Процедура ПоказатьНаКартеПоследнееМесто(Команда)
	СинхронизироватьНажатие();
	// Проверяем, выделена ли строка
	ТекущаяСтрока = Элементы.ЗаписанныеПоездки.ТекущиеДанные;
	Если ТекущаяСтрока = Неопределено Тогда
		ПоказатьПредупреждение(, "Выберите поездку в таблице!");
		Возврат;
	КонецЕсли;
	
	// Получаем поездку из текущей строки
	Поездка = ТекущаяСтрока.Поездка;
	
	СтатусПоездки = ТекущаяСтрока.Статус;
	
	Если СтатусПоездки = "В пути" Тогда
		
		// Получаем последние координаты для поездки
		Координаты = ПолучитьПоследниеКоординатыПоездки(Поездка);
	
		#Если МобильноеПриложениеКлиент Тогда
			Если Координаты <> Неопределено Тогда
				Если ПоддерживаетсяОтображениеКарты() Тогда
					// Отображаем точку на карте
					ПоказатьНаКартеМестоМаршрут(Координаты, Поездка);
				Иначе
					ПоказатьПредупреждение(, НСтр("ru='Отображение карты не поддерживается на устройстве!'"));
				КонецЕсли;
			Иначе
				ПоказатьПредупреждение(, НСтр("ru='Для выбранной поездки нет данных о местоположении!'"));
			КонецЕсли;
		#Иначе
			ПоказатьПредупреждение(, НСтр("ru='Функция доступна только в мобильном приложении!'"));
		#КонецЕсли
		
	Иначе 
		ПоказатьПредупреждение(, "Эта функция доступна, когда трамспорт в пути!");	
	КонецЕсли;
	
КонецПроцедуры

// Процедура, которая вызывает карту с отображением маршрута и текущее местоположения пользователя
// водителя, эта процедура доступна только для пассажиров
&НаКлиенте
Процедура ПоказатьНаКартеМестоМаршрут(Координаты, Поездка) Экспорт
    #Если МобильноеПриложениеКлиент Тогда
        Попытка
            // Проверка координат точки
            Если Координаты = Неопределено 
                Или Координаты.Широта < -90 Или Координаты.Широта > 90 
                Или Координаты.Долгота < -180 Или Координаты.Долгота > 180 Тогда
                ПоказатьПредупреждение(, НСтр("ru='Некорректные координаты!'"));
                Возврат;
            КонецЕсли;

            // Получаем данные маршрута
            ДанныеМаршрута = ПолучитьДанныеМаршрута(Поездка);
            Если ДанныеМаршрута = Неопределено Или ДанныеМаршрута.Количество() = 0 Тогда
                ПоказатьПредупреждение(, НСтр("ru='Для выбранной поездки нет данных маршрута!'"));
                Возврат;
            КонецЕсли;

            // Формируем URL с маршрутом и точкой
            GoogleMapsURL = ОпределениеПоложения.СформироватьURLМаршрутаТочка(ДанныеМаршрута, Координаты);
            Если ПустаяСтрока(GoogleMapsURL) Тогда
                ПоказатьПредупреждение(, НСтр("ru='Не удалось сформировать маршрут!'"));
                Возврат;
            КонецЕсли;

            // Запускаем Google Maps
            Запуск = Новый ЗапускПриложенияМобильногоУстройства("android.intent.action.VIEW", GoogleMapsURL);
            Запуск.Запустить(Ложь);

        Исключение
            ПоказатьПредупреждение(, НСтр("ru='Ошибка отображения карты: " + ОписаниеОшибки() + "'"));
        КонецПопытки;
    #Иначе
        ПоказатьПредупреждение(, НСтр("ru='Функция доступна только в мобильном приложении!'"));
    #КонецЕсли
КонецПроцедуры

// Серверная процедура для получения последних координат
&НаСервереБезКонтекста
Функция ПолучитьПоследниеКоординатыПоездки(Поездка)
	// Запрос к регистру сведений для получения последней записи
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ ПЕРВЫЕ 1
	|	КоординатыПоездок.Координаты КАК Координаты
	|ИЗ
	|	РегистрСведений.КоординатыПоездок КАК КоординатыПоездок
	|ГДЕ
	|	КоординатыПоездок.Поездка = &Поездка
	|УПОРЯДОЧИТЬ ПО
	|	КоординатыПоездок.ВремяГеопозиции УБЫВ";
	
	Запрос.УстановитьПараметр("Поездка", Поездка);
	
	Результат = Запрос.Выполнить();
	Если Результат.Пустой() Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	Выборка = Результат.Выбрать();
	Выборка.Следующий();
	
	// Извлекаем координаты из хранилища значения
	Возврат Выборка.Координаты.Получить();
КонецФункции

// Обработчик записи на поездку
&НаКлиенте
Процедура ЗаписатьсяНаПоездку(Команда)
	
	СинхронизироватьНажатие();
	
	// Запись на поездку возможна только когда пользователь в сети
	Если ВыполнитьПроверкуСети() Тогда 
		ЗаписьНаПоездкуБезПроверкиСети();	
	Иначе 
		ПоказатьПредупреждение(, НСтр("ru='Нет сети, нет возможности зпаисаться на поездку!'"));
	КонецЕсли;

КонецПроцедуры

// Осуществление записи на поездку
&НаКлиенте
Процедура ЗаписьНаПоездкуБезПроверкиСети()
	
	ПараметрыФормы = Новый Структура;
    ПараметрыФормы.Вставить("Пользователь", ТекущийПользователь);
	ОткрытьФорму("ОбщаяФорма.ЗаписьНаПоездку", ПараметрыФормы);
	
КонецПроцедуры

// Проверка доступности подключения к центральной базе данных
&НаСервере
Функция ВыполнитьПроверкуСети()
	Возврат СетевоеСоединение.ПроверитьСвязьССервером();
КонецФункции

// Обработчик отмены поездки, осущесвляется переход на форму, где нужно выбрать причину отмены поездки
&НаКлиенте
Процедура ОтменитьПоездку(Команда)
	СинхронизироватьНажатие();
	
	// Выполняется проверка сети, отменить поезщдку можно только если есть доступ к центральной базе данных
	Если ВыполнитьПроверкуСети() Тогда
		ТекущиеДанные = Элементы.ЗаписанныеПоездки.ТекущиеДанные;
		Если ТекущиеДанные = Неопределено Тогда
			ПоказатьПредупреждение(, "Выберите поездку!");
			Возврат;
	КонецЕсли;
	
	ОтменаПоездок.ОтменитьПоездкуБезПроверкиСети(ТекущиеДанные.Поездка, ТекущийПользователь, ТекущиеДанные.Статус);
	
	Иначе 
		ПоказатьПредупреждение(, НСтр("ru='Нет сети, нет возможности отменить поездку!'"));
	КонецЕсли;
	
КонецПроцедуры

// Показывает маршрут для конкретного пользователя
&НаКлиенте
Процедура ОтобразитьМаршрут(Команда)
	
	// Объмен данными с сервером
	СинхронизироватьНажатие();
	ТекущаяСтрока = Элементы.ЗаписанныеПоездки.ТекущиеДанные;
	
	Если ТекущаяСтрока = Неопределено Тогда
		ПоказатьПредупреждение(, НСтр("ru='Выберите поездку в таблице!'"));
		Возврат;
	КонецЕсли;
	
	Если НЕ ДоступноПодключениеКИнтернету() Тогда
		ПоказатьПредупреждение(, НСтр("ru='Нет интернет соединения!'"));
		Возврат;
	КонецЕсли;
	
	Поездка = ТекущаяСтрока.Поездка;
	
	#Если МобильноеПриложениеКлиент Тогда
		// Получаем данные маршрута
		ДанныеМаршрута = СобратьДанныеМаршрута(Поездка, ТекущийПользователь);
		
		Если ДанныеМаршрута = Неопределено ИЛИ ДанныеМаршрута.Количество() < 2 Тогда
			ПоказатьПредупреждение(, НСтр("ru='Для выбранной поездки нет данных маршрута!'"));
			Возврат;
		КонецЕсли;
		
		// Формируем URL и начальный адрес для Google Maps
		РезультатURL = ОпределениеПоложения.СоздатьURLМаршрутаМеждуТочками(ДанныеМаршрута);
		URL = РезультатURL.URL;
		НачальныйАдрес = РезультатURL.Начало;
		
		Если НЕ ЗначениеЗаполнено(URL) Тогда
			ПоказатьПредупреждение(, НСтр("ru='Не удалось построить маршрут: недостаточно точек!'"));
			Возврат;
		КонецЕсли;
		
		// Запускаем приложение карт
		Запуск = Новый ЗапускПриложенияМобильногоУстройства("android.intent.action.VIEW", URL);
		Попытка
			Запуск.Запустить(Ложь);
		Исключение
			
			ТекстОшибки = НСтр("ru='Ошибка запуска Google Maps: '") + ОписаниеОшибки();
            ПоказатьПредупреждение(, ТекстОшибки);
			
		КонецПопытки;
	#КонецЕсли
КонецПроцедуры

// Функция выдает данные с точками маршрута, включает точки от начальной выбранной точки до конечной для конкретного пользователя
&НаСервереБезКонтекста
Функция СобратьДанныеМаршрута(Поездка, ТекущийПользователь)
	// Запрос для получения остановок посадки, высадки и маршрута
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ПоездкаЗабронированныеМеста.ОстановкаПосадки КАК ОстановкаПосадки,
		|	ПоездкаЗабронированныеМеста.ОстановкаВысадки КАК ОстановкаВысадки
		|ПОМЕСТИТЬ Бронь
		|ИЗ
		|	Документ.Поездка.ЗабронированныеМеста КАК ПоездкаЗабронированныеМеста
		|ГДЕ
		|	ПоездкаЗабронированныеМеста.Ссылка = &Поездка
		|	И ПоездкаЗабронированныеМеста.Пассажир = &ТекущийПользователь
		|	И ПоездкаЗабронированныеМеста.БроньОтменена = ЛОЖЬ
		|;
		|
		|ВЫБРАТЬ
		|	ПоездкаМаршрутПоездки.Адрес КАК Адрес,
		|	ПоездкаМаршрутПоездки.Время КАК Время,
		|	ПоездкаМаршрутПоездки.НомерСтроки КАК НомерСтроки,
		|	ВЫБОР
		|		КОГДА ПоездкаМаршрутПоездки.Адрес = Бронь.ОстановкаПосадки
		|			ТОГДА ИСТИНА
		|		ИНАЧЕ ЛОЖЬ
		|	КОНЕЦ КАК ЭтоПосадка,
		|	ВЫБОР
		|		КОГДА ПоездкаМаршрутПоездки.Адрес = Бронь.ОстановкаВысадки
		|			ТОГДА ИСТИНА
		|		ИНАЧЕ ЛОЖЬ
		|	КОНЕЦ КАК ЭтоВысадка
		|ИЗ
		|	Документ.Поездка.МаршрутПоездки КАК ПоездкаМаршрутПоездки
		|		ЛЕВОЕ СОЕДИНЕНИЕ Бронь КАК Бронь
		|		ПО ИСТИНА
		|ГДЕ
		|	ПоездкаМаршрутПоездки.Ссылка = &Поездка";
	
	Запрос.УстановитьПараметр("Поездка", Поездка);
	Запрос.УстановитьПараметр("ТекущийПользователь", ТекущийПользователь);
	
	Результат = Запрос.Выполнить();
	Если Результат.Пустой() Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	// Формируем массив адресов
	ДанныеМаршрута = Новый Массив;
	Выборка = Результат.Выбрать();
	
	ВремяПосадки = Дата(1,1,1);
	ОстановкаПосадки = Неопределено;
	ОстановкаВысадки = Неопределено;
	ВключатьТочки = Ложь;
	
	// Находим время посадки и остановки
	Пока Выборка.Следующий() Цикл
		Если Выборка.ЭтоПосадка Тогда
			ВремяПосадки = Выборка.Время;
			ОстановкаПосадки = Выборка.Адрес;
		КонецЕсли;
		Если Выборка.ЭтоВысадка Тогда
			ОстановкаВысадки = Выборка.Адрес;
		КонецЕсли;
	КонецЦикла;
	
	// Проверка наличия брони
	Если ОстановкаПосадки = Неопределено ИЛИ ОстановкаВысадки = Неопределено Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	// Формируем маршрут
	Выборка.Сбросить();
	Пока Выборка.Следующий() Цикл
		// Начинаем с остановки посадки
		Если Выборка.Адрес = ОстановкаПосадки Тогда
			ВключатьТочки = Истина;
			СтруктураАдреса = ИзвлечьСтруктуруАдреса(Выборка.Адрес);
			Если СтруктураАдреса = Неопределено Тогда
				Продолжить;
			КонецЕсли;
			ДанныеМаршрута.Добавить(СтруктураАдреса);
		// Промежуточные точки
		ИначеЕсли ВключатьТочки И Выборка.Время > ВремяПосадки И Выборка.Адрес <> ОстановкаВысадки Тогда
			СтруктураАдреса = ИзвлечьСтруктуруАдреса(Выборка.Адрес);
			Если СтруктураАдреса = Неопределено Тогда
				Продолжить;
			КонецЕсли;
			ДанныеМаршрута.Добавить(СтруктураАдреса);

		// Остановка высадки
		ИначеЕсли Выборка.Адрес = ОстановкаВысадки Тогда
			СтруктураАдреса = ИзвлечьСтруктуруАдреса(Выборка.Адрес);
			Если СтруктураАдреса = Неопределено Тогда
				Продолжить;
			КонецЕсли;
			ДанныеМаршрута.Добавить(СтруктураАдреса);
			ВключатьТочки = Ложь;
		КонецЕсли;
	КонецЦикла;
	
	// Проверка маршрута
	Если ДанныеМаршрута.Количество() < 2 Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	Возврат ДанныеМаршрута;
КонецФункции

// Функция возвращает данные адреса, который является начальной точкой маршрута
&НаСервереБезКонтекста
Функция ИзвлечьСтруктуруАдреса(АдресСсылка)
	// Получение данных адреса из справочника
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	Адреса.Страна,
		|	Адреса.Регион,
		|	Адреса.Город,
		|	Адреса.Улица,
		|	Адреса.Дом,
		|	Адреса.Индекс
		|ИЗ
		|	Справочник.Адреса КАК Адреса
		|ГДЕ
		|	Адреса.Ссылка = &Адрес";
	
	Запрос.УстановитьПараметр("Адрес", АдресСсылка);
	Выборка = Запрос.Выполнить().Выбрать();
	
	Если Не Выборка.Следующий() Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	// Проверяем обязательность поля Город
	Если НЕ ЗначениеЗаполнено(Выборка.Город) Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	СтруктураАдреса = Новый Структура;
	СтруктураАдреса.Вставить("Страна", ?(ЗначениеЗаполнено(Выборка.Страна), Выборка.Страна, ""));
	СтруктураАдреса.Вставить("Регион", ?(ЗначениеЗаполнено(Выборка.Регион), Выборка.Регион, ""));
	СтруктураАдреса.Вставить("Город", Выборка.Город);
	СтруктураАдреса.Вставить("Улица", ?(ЗначениеЗаполнено(Выборка.Улица), Выборка.Улица, ""));
	СтруктураАдреса.Вставить("Дом", ?(ЗначениеЗаполнено(Выборка.Дом), Выборка.Дом, ""));
	СтруктураАдреса.Вставить("Индекс", ?(ЗначениеЗаполнено(Выборка.Индекс), Выборка.Индекс, ""));
	
	Возврат СтруктураАдреса;
КонецФункции

// Переход на форму для отображения полной информации для выбранной поездки
&НаКлиенте
Процедура РасширеннаяИнформация(Команда)
	ТекущаяСтрока = Элементы.ЗаписанныеПоездки.ТекущиеДанные;

	// Проверка выбора поездки
	Если ТекущаяСтрока = Неопределено Тогда
		ПоказатьПредупреждение(, НСтр("ru='Выберите поездку!'"));
		Возврат;
	КонецЕсли;
	
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("Поездка", ТекущаяСтрока.Поездка);
	ПараметрыФормы.Вставить("Статус", ТекущаяСтрока.Статус);
	ПараметрыФормы.Вставить("ТекущийПользователь", ТекущийПользователь);
	
	// Открытие формы
	ОткрытьФорму("ОбщаяФорма.РасширеннаяИнформацияПоездкиЗабронированной", ПараметрыФормы, ЭтотОбъект);
КонецПроцедуры

// Создание локального уведомления
&НаКлиенте
Асинх Процедура СоздатьНапоминаниеНажатие(Элемент)
    #Если МобильноеПриложениеКлиент Тогда
        // Проверяем, выбрана ли строка
        ТекущаяСтрока = Элементы.ЗаписанныеПоездки.ТекущиеДанные;
        
        Если ТекущаяСтрока = Неопределено Тогда
            ПоказатьПредупреждение(Неопределено, НСтр("ru='Выберите поездку!'"));
            Возврат;
		КонецЕсли;
		
		Если ТекущаяСтрока.Статус = "В пути" Тогда
            ПоказатьПредупреждение(Неопределено, НСтр("ru='Поездка уже началась!'"));
            Возврат;
        КонецЕсли;
   
        Попытка
            ДатаУведомления = Ждать ВвестиДатуАсинх(ТекущаяДата(), "Введите дату и время уведомления", ЧастиДаты.ДатаВремя);
			
			// Проверка 1: Дата уведомления не должна быть меньше текущей
        	ТекущаяДатаВремя = ТекущаяДата();
        	Если ДатаУведомления < ТекущаяДатаВремя Тогда
            	ПоказатьПредупреждение(Неопределено, НСтр("ru='Дата уведомления не может быть меньше текущей даты и времени!'"));
            	Возврат;
        	КонецЕсли;
        
        	// Проверка 2: Разница между текущей датой и датой уведомления минимум 5 минут
        	РазницаВСекундах = ДатаУведомления - ТекущаяДатаВремя;
        	Если РазницаВСекундах < 300 Тогда // 300 секунд = 5 минут
            	ПоказатьПредупреждение(Неопределено, НСтр("ru='Дата уведомления должна быть минимум на 5 минут позже текущей даты!'"));
            	Возврат;
        	КонецЕсли;
        
        	// Проверка 3: Дата уведомления не должна быть больше даты поездки
        	ДатаПоездки = ПолучитьДатуПоездкиИзДокумента(ТекущаяСтрока.Поездка);
        	ВремяПосадки = ПолучитьВремяОстановкиПосадки(ТекущаяСтрока.Поездка);
        	Если ДатаПоездки = Дата(1,1,1) Или ВремяПосадки = Дата(1,1,1) Тогда
            	ПоказатьПредупреждение(Неопределено, НСтр("ru='Не удалось определить дату или время поездки!'"));
            	Возврат;
        	КонецЕсли;
            
            Уведомление = Новый ДоставляемоеУведомление;
            Уведомление.Заголовок = "Запланирована поездка";
			Уведомление.Текст = ПолучитьСообщениеОПоездке(ПолучитьДатуПоездкиИзДокумента(ТекущаяСтрока.Поездка), ПолучитьВремяОстановкиПосадки(ТекущаяСтрока.Поездка));
            Уведомление.ДатаПоявленияУниверсальноеВремя = ДатаОтправки(ДатаУведомления);
			Уведомление.Данные = "Trip_" + Строка(ТекущаяСтрока.Поездка.УникальныйИдентификатор()) + "_" + Формат(ДатаУведомления, "ДФ=yyyyMMddHHmmss");
            
            ДоставляемыеУведомления.ДобавитьЛокальноеУведомление(Уведомление);
			СоздатьЗаписьЛокальногоУведомления(ТекущийПользователь, Уведомление.Данные, ТекущаяСтрока.Поездка);
            
        Исключение
			ПоказатьПредупреждение(, НСтр("ru='" + "Ошибка при создании напоминания: " + ОписаниеОшибки() + "'"));
        КонецПопытки
    #Иначе
		ПоказатьПредупреждение(, НСтр("ru='" + "Работа с уведомлениями не поддерживается в данном контексте!" + "'"));
    #КонецЕсли
КонецПроцедуры

// Получение разницы дат, чтобы дата поездки не была позже текущей даты
&НаСервере
Функция ДатаОтправки(ДатаУведомления)
    РазницаДат = ДатаУведомления - ТекущаяДата();
    Возврат ТекущаяУниверсальнаяДата() + РазницаДат;
КонецФункции

// Получение даты поездки из документа, необходимо для проверки времени прихода уведомления
&НаСервере
Функция ПолучитьДатуПоездкиИзДокумента(Поездка)
    Запрос = Новый Запрос;
    Запрос.Текст = 
        "ВЫБРАТЬ
        |   Поездка.ДатаПоездки КАК ДатаПоездки
        |ИЗ
        |   Документ.Поездка КАК Поездка
        |ГДЕ
        |   Поездка.Ссылка = &Поездка";
    
    Запрос.УстановитьПараметр("Поездка", Поездка);
    
    РезультатЗапроса = Запрос.Выполнить();
	
	// Пустая дата вместо исключения
    Если РезультатЗапроса.Пустой() Тогда
        Возврат Дата(1,1,1); 
    КонецЕсли;
    
    Выборка = РезультатЗапроса.Выбрать();
    Выборка.Следующий();
    
    Возврат Выборка.ДатаПоездки;
КонецФункции

// Получение времени остановки и посадки, необходимо для контроля создания уведомления
// уведомление не должно прийти позже начала поездки
&НаСервере
Функция ПолучитьВремяОстановкиПосадки(Поездка)
    // Нахождение адреса остановки и посадки для текущего пользователя
    Запрос = Новый Запрос;
    Запрос.Текст = 
        "ВЫБРАТЬ
        |   ПоездкаЗабронированныеМеста.ОстановкаПосадки КАК ОстановкаПосадки
        |ИЗ
        |   Документ.Поездка.ЗабронированныеМеста КАК ПоездкаЗабронированныеМеста
        |ГДЕ
        |   ПоездкаЗабронированныеМеста.Ссылка = &Поездка
        |   И ПоездкаЗабронированныеМеста.Пассажир = &ТекущийПользователь";
    
    Запрос.УстановитьПараметр("Поездка", Поездка);
    Запрос.УстановитьПараметр("ТекущийПользователь", ТекущийПользователь);
    
    РезультатЗапроса = Запрос.Выполнить();
    
	Если РезультатЗапроса.Пустой() Тогда
		// Пустая дата, если адрес не найден
        Возврат Дата(1,1,1); 
    КонецЕсли;
    
    Выборка = РезультатЗапроса.Выбрать();
    Выборка.Следующий();
    ОстановкаПосадки = Выборка.ОстановкаПосадки;
    
    // Находим время для адреса остановки и посадки
    Запрос = Новый Запрос;
    Запрос.Текст = 
        "ВЫБРАТЬ
        |   ПоездкаМаршрутПоездки.Время КАК Время
        |ИЗ
        |   Документ.Поездка.МаршрутПоездки КАК ПоездкаМаршрутПоездки
        |ГДЕ
        |   ПоездкаМаршрутПоездки.Ссылка = &Поездка
        |   И ПоездкаМаршрутПоездки.Адрес = &ОстановкаПосадки";
    
    Запрос.УстановитьПараметр("Поездка", Поездка);
    Запрос.УстановитьПараметр("ОстановкаПосадки", ОстановкаПосадки);
    
    РезультатЗапроса = Запрос.Выполнить();
	
	// Пустая дата вместо исключения
    Если РезультатЗапроса.Пустой() Тогда
        Возврат Дата(1,1,1); 
    КонецЕсли;
    
    Выборка = РезультатЗапроса.Выбрать();
    Выборка.Следующий();
    
    Возврат Выборка.Время;
КонецФункции

// Обработчик локального уведомления
&НаСервере
Функция ПолучитьСообщениеОПоездке(ДатаПоездки, ВремяПосадки)
    // Объединяем дату поездки и время посадки
    ДатаВремяПосадки = Дата(Год(ДатаПоездки), Месяц(ДатаПоездки), День(ДатаПоездки),
                            Час(ВремяПосадки), Минута(ВремяПосадки), Секунда(ВремяПосадки));
    
    // Форматируем дату и время в строку
    ФорматированнаяДата = Формат(ДатаВремяПосадки, "ДЛФ=DT");
    
    // Формируем локализованное сообщение
    Сообщение = НСтр("ru='У вас запланирована поездка на %1'");
    Сообщение = СтрШаблон(Сообщение, ФорматированнаяДата);
    
    Возврат Сообщение;
КонецФункции

// Создание локального уведомление, каждому уведомлению присваивается идентифкатор
&НаСервере
Процедура СоздатьЗаписьЛокальногоУведомления(Пассажир, ИдентификаторУведомления, Поездка)
    НовыйЭлемент = Справочники.ЛокальныеУведомления.СоздатьЭлемент();
    НовыйЭлемент.Пассажир = Пассажир;
    НовыйЭлемент.ИдентификаторУведомления = ИдентификаторУведомления;
    НовыйЭлемент.Поездка = Поездка;
	НовыйЭлемент.Наименование = "Пользователь_" + Пассажир;
    
    Попытка
        НовыйЭлемент.Записать();
    Исключение
        Сообщить("Ошибка при записи в справочник ЛокальныеУведомления: " + ОписаниеОшибки());
    КонецПопытки;
КонецПроцедуры