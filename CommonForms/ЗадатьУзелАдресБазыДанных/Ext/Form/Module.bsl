// Форма для ввода и редактирования данных для подключения к серверу и обмена данными с ним

// Зафиксировать изменения
&НаКлиенте
Процедура Готово(Команда)
    
    // Проверка заполненности полей
    Если ПустаяСтрока(НаборКонстант.КодУзлаЦентральнойБазы) Тогда
        ПоказатьПредупреждение(, "Заполните поле Код узла центральной базы!");
        Возврат;
    КонецЕсли;
    
    Если ПустаяСтрока(НаборКонстант.КодНовогоУзлаПланаОбмена) Тогда
        ПоказатьПредупреждение(, "Заполните поле Код нового узла плана обмена!");
        Возврат;
    КонецЕсли;
    
    Если ПустаяСтрока(НаборКонстант.АдресЦентральнойБазы) Тогда
        ПоказатьПредупреждение(, "Заполните поле Адрес центральной базы!");
        Возврат;
	КонецЕсли;
	
	Если ПустаяСтрока(НаборКонстант.НазваниеУзлаУстройства) Тогда
        ПоказатьПредупреждение(, "Заполните поле Название узла устройства!");
        Возврат;
	КонецЕсли;
	
	Если ПустаяСтрока(НаборКонстант.НазваниеЦентральногоУзла) Тогда
        ПоказатьПредупреждение(, "Заполните поле Название центрального узла!");
        Возврат;
    КонецЕсли;
        
    Попытка        
        ЗаписатьКонстантыИОбновитьПланОбменаНаСервере();
        ПоказатьПредупреждение(, "Данные успешно сохранены!");
        Закрыть(); // Закрываем форму
    Исключение
        ПоказатьПредупреждение(, "Ошибка при сохранении данных: " + ОписаниеОшибки());
    КонецПопытки;
КонецПроцедуры

// Обработчик закрытия формы
&НаКлиенте
Процедура ПриЗакрытии(ЗавершениеРаботы)
    // Отключаем запрос на сохранение изменений при закрытии формы
    Если Модифицированность Тогда
        Модифицированность = Ложь; // Сбрасываем модифицированность
    КонецЕсли;
КонецПроцедуры

// Процедура записи введенных данных в соответсвующие константы
&НаСервере
Процедура ЗаписатьКонстантыИОбновитьПланОбменаНаСервере()
    
    // Запись значений констант
    Попытка
        Константы.КодУзлаЦентральнойБазы.Установить(НаборКонстант.КодУзлаЦентральнойБазы);
        Константы.КодНовогоУзлаПланаОбмена.Установить(НаборКонстант.КодНовогоУзлаПланаОбмена);
        Константы.АдресЦентральнойБазы.Установить(НаборКонстант.АдресЦентральнойБазы);
	    Константы.НазваниеЦентральногоУзла.Установить(НаборКонстант.НазваниеЦентральногоУзла);
        Константы.НазваниеУзлаУстройства.Установить(НаборКонстант.НазваниеУзлаУстройства);
		Константы.ПространствоИменВебСервиса.Установить(НаборКонстант.ПространствоИменВебСервиса);
        // Сбрасываем модифицированность формы после сохранения
        Модифицированность = Ложь;
    Исключение
        ВызватьИсключение "Не удалось записать константы: " + ОписаниеОшибки();
	КонецПопытки;
	
	// Обновление плана обмена "Мобильные"
    Попытка
        // Обновление главного узла текущей базы
        ГлавныйУзел = ПланыОбмена.Мобильные.ЭтотУзел();
        Если ГлавныйУзел <> ПланыОбмена.Мобильные.ПустаяСсылка() Тогда
            ГлавныйУзелОбъект = ГлавныйУзел.ПолучитьОбъект();
            ГлавныйУзелОбъект.Код = НаборКонстант.КодНовогоУзлаПланаОбмена;
            ГлавныйУзелОбъект.Наименование = НаборКонстант.НазваниеУзлаУстройства;
            ГлавныйУзелОбъект.Записать();
        КонецЕсли;
        
        // Проверка и создание узла центральной базы
        УзелЦентральнойБазы = ПланыОбмена.Мобильные.НайтиПоКоду(НаборКонстант.КодУзлаЦентральнойБазы);
        Если УзелЦентральнойБазы.Пустая() Тогда
            НовыйУзел = ПланыОбмена.Мобильные.СоздатьУзел();
            НовыйУзел.Код = НаборКонстант.КодУзлаЦентральнойБазы;
            НовыйУзел.Наименование = НаборКонстант.НазваниеЦентральногоУзла;
            НовыйУзел.Записать();
        КонецЕсли;
        
        // Сбрасываем модифицированность формы после сохранения
        Модифицированность = Ложь;
    Исключение
        ВызватьИсключение "Не удалось обновить план обмена: " + ОписаниеОшибки();
    КонецПопытки;
    
КонецПроцедуры